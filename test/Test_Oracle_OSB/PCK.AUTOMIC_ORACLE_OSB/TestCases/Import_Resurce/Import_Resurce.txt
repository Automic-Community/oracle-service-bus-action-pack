*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        import_resource
Default Tags
Test Template     Import Resource Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    SessionName        FileToImport                PassPhrase
Import Resource with Valid Inputs when Pass Phrase is provided
                      OK        ${Session_Name}    ${Resource_Path}            Generated

Import Resource with Valid Inputs when Pass Phrase is not provided
                      OK        ${Session_Name}    ${Resource_Path}            ${EMPTY}

Assertion with Non-Existing Session Name
                      NOK       Invalid            ${Resource_Path}            ${EMPTY}

Assertion with Invalid Path of File to Import
                      NOK       ${Session_Name}    Invalid                     ${EMPTY}

Assertion with Invalid type of File to Import
                      NOK       ${Session_Name}    ${Invalid_Resource_File}    ${EMPTY}

Assert Session Name As a Mandatory Field
                      OTHER     ${EMPTY}           ${Resource_Path}            Generated

Assert File to Import As a Mandatory Field
                      OTHER     ${Session_Name}    ${EMPTY}                    Generated

*** Keywords ***
Startup
    Log    *******Start Test "IMPORT RESOURCE"*******
    Log    *******Connect to AE*******
    Connect AE

Import Resource Template
    [Arguments]    ${OK_NOK}    ${SessionName}    ${FileToImport}    ${PassPhrase}
    [Documentation]    This test will import a resource to OSB.
    ...    -Precondition: To run this test suite OSB credentials should be known to user. To run the test cases on UNIX change the values of Hostname and other fields accordingly. Also change inputs for resource file and invalid resource file accordingly.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK, NOK or OTHER.
    ...    -${SessionName}: This field specifies name of the session which already exists.
    ...    -${FileToImport}: This field specifies path to the resource which you want to import.
    ...    -${PassPhrase}: This field specifies password to the resource whihc is imported.
    Generate String
    Action Create    ${_IMPORT_RESOURCE_ACTION}
    Common Inputs
    Run Keyword If    '${SessionName}' == 'Invalid'    Action Set    &UC4RB_OSB_SESSION_NAME#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_OSB_SESSION_NAME#    ${SessionName}
    Run Keyword If    '${FileToImport}' == 'Invalid'    Action Set    &UC4RB_OSB_FILE_PATH#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_OSB_FILE_PATH#    ${FileToImport}
    Run Keyword If    '${PassPhrase}' == 'Generated'    Action Set    &UC4RB_OSB_PASS_PHRASE#    ${Generated_Name}    ELSE    Action Set
    ...    &UC4RB_OSB_PASS_PHRASE#    ${PassPhrase}
    Set Overwrite Agent
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE IF    '${OK_NOK}' == 'NOK'    Assert Failure    ELSE
    ...    Assert Mandatory

Teardown
    Log    *******End Test "IMPORT RESOURCE"*******

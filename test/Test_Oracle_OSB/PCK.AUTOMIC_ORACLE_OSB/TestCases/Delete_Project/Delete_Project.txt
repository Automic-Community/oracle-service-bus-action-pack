*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        delete_project
Default Tags
Test Template     Delete Project Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    ProjectName        SessionName
Delete Project With Valid Inputs
                      [Tags]    main_scenarios
                      OK        ${Project_Name}    ${Session_Name}

Assertion With Non-Existing Project Name
                      [Tags]    main_scenarios
                      NOK       Invalid            ${Session_Name}

Assertion With Non-Existing Session Name
                      [Tags]    main_scenarios
                      NOK       ${Project_Name}    Invalid

Assert Project Name As a Mandatory Field
                      [Tags]    main_scenarios
                      OTHER     ${EMPTY}           ${Session_Name}

Assert Session Name As a Mandatory Field
                      [Tags]    main_scenarios
                      OTHER     ${Project_Name}    ${EMPTY}

*** Keywords ***
Startup
    Log    *******Start Test "DELETE PROJECT"*******
    Log    *******Connect to AE*******
    Connect AE

Delete Project Template
    [Arguments]    ${OK_NOK}    ${ProjectName}    ${SessionName}
    [Documentation]    This test deletes the already existing project on OSB.
    ...    -Precondition: To run these test cases OSB credentials and existing session name and project name should be known to user. To run the test cases on UNIX change the values of OSB credentials and other fields accordingly in variable.txt file.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK, NOK or OTHER.
    ...    -${ProjectName}: This field specifies name of the project you want to delete..
    ...    -${SessionName}: This field specifies name of the session you want to use for this operation.
    Generate String
    Action Create    ${_DELETE_PROJECT_ACTION}
    Common Inputs
    Run Keyword If    '${ProjectName}' == 'Invalid'    Action Set    &UC4RB_OSB_PROJECT_NAME#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_OSB_PROJECT_NAME#    ${ProjectName}
    Run Keyword If    '${SessionName}' == 'Invalid'    Action Set    &UC4RB_OSB_SESSION_NAME#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_OSB_SESSION_NAME#    ${SessionName}
    Set Overwrite Agent
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE IF    '${OK_NOK}' == 'NOK'    Assert Failure    ELSE
    ...    Assert Mandatory

Teardown
    Log    *******End Test "DELETE PROJECT"*******

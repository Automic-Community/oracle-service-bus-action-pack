*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        common_failures
Default Tags
Test Template     Common Failures Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    SudoPassword        Options    SudoUsername        OSBHome        JavaOptions        Protocol           Hostname           Username           Password           Port           SessionName
Assertion with Invalid OSB Home
                      NOK       ${Sudo_Password}    None       ${Sudo_Username}    Invalid        ${EMPTY}           ${OSB_Protocol}    ${OSB_Hostname}    ${OSB_Username}    ${OSB_Password}    ${OSB_Port}    ${Session_Name}

Assertion with Invalid Java Options
                      NOK       ${Sudo_Password}    None       ${Sudo_Username}    ${OSB_Home}    Invalid            ${OSB_Protocol}    ${OSB_Hostname}    ${OSB_Username}    ${OSB_Password}    ${OSB_Port}    ${Session_Name}

Assertion with Invalid Protocol
                      NOK       ${EMPTY}            None       ${EMPTY}            ${OSB_Home}    ${EMPTY}           https              ${OSB_Hostname}    ${OSB_Username}    ${OSB_Password}    ${OSB_Port}    ${Session_Name}

Assertion with Non-Existing Hostname
                      NOK       ${EMPTY}            None       ${EMPTY}            ${OSB_Home}    ${EMPTY}           ${OSB_Protocol}    Invalid            ${OSB_Username}    ${OSB_Password}    ${OSB_Port}    ${Session_Name}

Assertion with Non-Existing Username
                      NOK       ${EMPTY}            None       ${EMPTY}            ${OSB_Home}    ${EMPTY}           ${OSB_Protocol}    ${OSB_Hostname}    Invalid            ${OSB_Password}    ${OSB_Port}    ${Session_Name}

Assertion with Invalid Password
                      NOK       ${EMPTY}            None       ${EMPTY}            ${OSB_Home}    ${EMPTY}           ${OSB_Protocol}    ${OSB_Hostname}    ${OSB_Username}    Invalid            ${OSB_Port}    ${Session_Name}

Assertion with Invalid Port
                      NOK       ${EMPTY}            None       ${EMPTY}            ${OSB_Home}    ${EMPTY}           ${OSB_Protocol}    ${OSB_Hostname}    ${OSB_Username}    ${OSB_Password}    3              ${Session_Name}

Assert OSB Home As a Mandatory Field
                      OTHER     ${Sudo_Password}    None       ${Sudo_Username}    ${EMPTY}       ${Java_Options}    ${OSB_Protocol}    ${OSB_Hostname}    ${OSB_Username}    ${OSB_Password}    ${OSB_Port}    ${Session_Name}

Assert Hostname As a Mandatory Field
                      OTHER     ${Sudo_Password}    None       ${Sudo_Username}    ${OSB_Home}    ${Java_Options}    ${OSB_Protocol}    ${EMPTY}           ${OSB_Username}    ${OSB_Password}    ${OSB_Port}    ${Session_Name}

Assert Username As a Mandatory Field
                      OTHER     ${Sudo_Password}    None       ${Sudo_Username}    ${OSB_Home}    ${Java_Options}    ${OSB_Protocol}    ${OSB_Hostname}    ${EMPTY}           ${OSB_Password}    ${OSB_Port}    ${Session_Name}

Assert Password As a Mandatory Field
                      OTHER     ${Sudo_Password}    None       ${Sudo_Username}    ${OSB_Home}    ${Java_Options}    ${OSB_Protocol}    ${OSB_Hostname}    ${OSB_Username}    ${EMPTY}           ${OSB_Port}    ${Session_Name}

*** Keywords ***
Startup
    Log    *******Start Test "COMMON FAILURES"*******
    Log    *******Connect to AE*******
    Connect AE

Common Failures Template
    [Arguments]    ${OK_NOK}    ${SudoPassword}    ${Options}    ${SudoUsername}    ${OSBHome}    ${JavaOptions}
    ...    ${Protocol}    ${Hostname}    ${Username}    ${Password}    ${Port}    ${SessionName}
    [Documentation]    This test will create a session on OSB.
    ...    -Precondition: To run this test suite OSB credentials should be known to user. To run the test cases on UNIX change the values of Hostname and other fields accordingly.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK, NOK or OTHER.
    ...    -${SudoPassword}: This field specifies password for the super user for unix environment.
    ...    -${Options}: This field specifies options to use like su or sudo.
    ...    -${SudoUsername}: This field specifies username for the super user for unix environment.
    ...    -${JavaOptions}: This field specifies the java options.
    ...    -${OSBHome}: This field specifies system environment variable for OSB.
    ...    -${Protocol}: This field specifies protocol over which you want to connect to OSB.
    ...    -${Hostname}: This field specifies hostname of OSB.
    ...    -${Username}: This field specifies username for OSB.
    ...    -${Password}: This field specifies the password for provided username.
    ...    -${Port}: This field specifies port on which OSB is running.
    ...    -${SessionName}: This field specifies name of the session you want to create.
    Generate String
    Action Create    ${_CREATE_SESSION_ACTION}
    Sudo User Details
    Set Home
    Connect to OSB
    Run Keyword If    '${SessionName}' == 'Invalid'    Action Set    &UC4RB_OSB_SESSION_NAME#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_OSB_SESSION_NAME#    ${SessionName}
    Set Overwrite Agent
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE IF    '${OK_NOK}' == 'NOK'    Assert Failure    ELSE
    ...    Assert Mandatory

Teardown
    Log    *******End Test "COMMON FAILURES"*******

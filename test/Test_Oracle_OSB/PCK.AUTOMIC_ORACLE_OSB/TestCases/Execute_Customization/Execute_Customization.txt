*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        execute_customization
Default Tags
Test Template     Execute Customization Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    SessionName        CustomizationFile
Execute Customization with Valid Inputs
                      OK        ${Session_Name}    ${Customization_File}

Assertion with Non-Existing Session Name
                      NOK       Invalid            ${Customization_File}

Assertion with Non-Existing Path to Customization File
                      NOK       ${Session_Name}    Invalid

Assertion with Invalid type of Customization File
                      NOK       ${Session_Name}    ${Invalid_Customization_File}

Assert Session Name As a Mandatory Field
                      OTHER     ${EMPTY}           ${Customization_File}

Assert Customization File As a Mandatory Field
                      OTHER     ${Session_Name}    ${EMPTY}

*** Keywords ***
Startup
    Log    *******Start Test "EXECUTE CUSTOMIZATION"*******
    Log    *******Connect to AE*******
    Connect AE

Execute Customization Template
    [Arguments]    ${OK_NOK}    ${SessionName}    ${CustomizationFile}
    [Documentation]    This test will execute customization file.
    ...    -Precondition: To run this test suite used session name should exist. To run the test cases on UNIX change the values of Hostname and other fields accordingly. Also change inputs for customization file and invalid customization file accordingly.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK, NOK or OTHER.
    ...    -${SessionName}: This field specifies name of the session which already exists.
    ...    -${CustomizationFile}: This field specifies path to the customization file.
    Generate String
    Action Create    ${_EXECUTE_CUSTOMIZATION_ACTION}
    Common Inputs
    Run Keyword If    '${SessionName}' == 'Invalid'    Action Set    &UC4RB_OSB_SESSION_NAME#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_OSB_SESSION_NAME#    ${SessionName}
    Run Keyword If    '${CustomizationFile}' == 'Invalid'    Action Set    &UC4RB_OSB_FILE_PATH#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_OSB_FILE_PATH#    ${CustomizationFile}
    Set Overwrite Agent
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE IF    '${OK_NOK}' == 'NOK'    Assert Failure    ELSE
    ...    Assert Mandatory

Teardown
    Log    *******End Test "EXECUTE CUSTOMIZATION"*******
